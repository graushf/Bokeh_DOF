<html>
    <head>
        <title>Bokeh_DOF</title>
        <meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

        <link rel="stylesheet" href="css/styles.css" type="text/css">
        <script type="text/javascript" src="js/webgl-utils.js"></script>
	    <script type="text/javascript" src="js/gl-matrix-test.js"></script>
        <script type="text/javascript" src="js/main.js"></script>
        <script type="text/javascript" src="js/Camera.js"></script>
        <script type="text/javascript" src="js/Geometries.js"></script>
        <script type="text/javascript" src="js/ObjectGeometryModels.js"></script>
        <script type="text/javascript" src="js/Renderer.js"></script>
        <script type="text/javascript" src="js/ShaderUtils.js"></script>
        <script type="text/javascript" src="js/Eventhandler.js"></script>
        <script type="text/javascript" src="js/K3D.js"></script>
        <script type="text/javascript" src="js/texture.js"></script>
        <script type="text/javascript" src="js/Framebuffers.js"></script>
        <script type="text/javascript" src="js/StatsInformation.js"></script>
        <script type="text/javascript" src="js/MRTFramebuffers.js"></script>

        <script id="phongLightingPass-fs" type="x-shader/x-fragment">
            precision mediump float;

            varying vec3 vFragPos;
            varying vec3 vNormal;
            varying vec2 vTextureCoord;

            uniform vec3 uStaticColor;

            uniform vec3 lightPos;
            uniform vec3 viewPos;
            uniform vec3 lightColor;

            uniform int uUseTexture;

            uniform int uDisableLighting;

            struct Material {
                sampler2D diffuse;
                vec3 diffuseColor;
                float shininess;
                int hasSpecular;
            };

            struct DirLight {
                vec3 direction;

                vec3 ambient;
                vec3 diffuse;
                vec3 specular;
            };

            struct PointLight {
                vec3 position;

                float constant;
                float linear;
                float quadratic;

                vec3 ambient;
                vec3 diffuse;
                vec3 specular;
            };

            #define NR_POINT_LIGHTS 1
            uniform PointLight pointLights[NR_POINT_LIGHTS];

            uniform DirLight dirLight;
            uniform Material material;

            vec3 CalcDirLight(DirLight light, vec3 normal, vec3 viewDir);
            vec3 CalCPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir);

            void main()
            {   
                vec3 result = uStaticColor;
                //gl_FragColor = vec4(0.0, 1.0, 0.0, 1.0); return;
                if (uDisableLighting == 0)
                {
                    vec3 norm = normalize(vNormal);
                    vec3 lightDir = normalize(lightPos - vFragPos);
                    vec3 viewDir = normalize(viewPos - vFragPos);

                    result = vec3(0.0, 0.0, 0.0);
                    result += CalcDirLight(dirLight, norm, viewDir);
                    //for (int i = 0; i < NR_POINT_LIGHTS; i++) {
                    //    result += CalcPointLights(pointsLights[i], norm, vFragPos, viewDir);
                    //}
                }

                //result = result * dot(vec3(0.3, 0.59, 0.11), result);
                //result = result * result;
                //result =  result * 1500.0;

                gl_FragColor = vec4(result, 1.0);
            }

            vec3 CalcDirLight(DirLight light, vec3 normal, vec3 viewDir) {
                vec3 lightDir = normalize(-light.direction);
                // diffuse shading
                float diff = max(dot(normal, lightDir), 0.0);
                // specular shading
                vec3 reflectDir = reflect(-lightDir, normal);
                float spec = 0.0;
                if (material.hasSpecular == 1) {
                    spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
                }

                // combine results
                vec3 ambient;
                vec3 diffuse;

                if (uUseTexture == 1) {
                    ambient = light.ambient * vec3(texture2D(material.diffuse, vTextureCoord));
                    diffuse = light.diffuse * diff * vec3(texture2D(material.diffuse, vTextureCoord));
                } else {
                    //ambient = light.ambient * light * diffuse;
                    //diffuse = light.diffuse * diff * light.diffuse;
                    ambient = light.ambient * material.diffuseColor;
                    diffuse = light.diffuse * diff * material.diffuseColor;
                }

                vec3 specular = light.specular * spec * light.diffuse;

                return (ambient + diffuse + specular);
            }

            vec3 CalcPointLight(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir) {
                vec3 lightDir = normalize(light.position - fragPos);

                // diffuse shading
                float diff = max(dot(normal, lightDir), 0.0);
                // specular shading
                vec3 reflectDir = reflect(-lightDir, normal);
                float spec = 0.0;
                if (material.hasSpecular == 1) {
                    spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
                }
                // attenuation
                float distance = length(light.position - fragPos);
                float attenuation = 1.0 / (light.constant + light.linear * distance + light.quadratic * (distance * distance));

                // combine results
                vec3 ambient = light.ambient * lightColor;
                vec3 diffuse = lightColor * diff * lightColor;
                vec3 specular = light.specular * spec * light.diffuse;
                ambient *= attenuation;
                diffuse *= attenuation;
                specular *= attenuation;
                return (diffuse + specular);
            }
            
        </script>

        <script id="phongLightingPass-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aTextureCoord;

            varying vec3 vNormal;
            varying vec3 vFragPos;
            varying vec2 vTextureCoord;

            uniform mat4 uMMatrix;
            uniform mat4 uVMatrix;
            uniform mat4 uPMatrix;

            void main(void) {
                gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(aVertexPosition, 1.0);
                vFragPos = vec3(uMMatrix * vec4(aVertexPosition, 1.0));
                vTextureCoord = aTextureCoord;
                vNormal = aVertexNormal;
            }
        </script>

        <script id="basic-shader-fs" type="x-shader/x-fragment">
            precision mediump float;

            varying vec3 vFragPos;
            varying vec3 vNormal;
            varying vec2 vTextureCoord;

            uniform vec3 uStaticColor;

            void main() {
                gl_FragColor = vec4(uStaticColor, 1.0);
            }
        </script>

        <script id="basic-shader-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec3 aVertexNormal;
            attribute vec2 aTextureCoord;

            varying vec3 vNormal;
            varying vec3 vFragPos;
            varying vec2 vTextureCoord;

            uniform mat4 uMMatrix;
            uniform mat4 uVMatrix;
            uniform mat4 uPMatrix;

            void main(void) {
                gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(aVertexPosition, 1.0);

                vFragPos = vec3(uMMatrix * vec4(aVertexPosition, 1.0));
                vTextureCoord = aTextureCoord;
                vNormal = aVertexNormal;
            }
        </script>

        <script id="drawBackgroundPass-fs" type="x-shader/x-fragment">
            precision mediump float;

            uniform vec3 uStaticColor;

            uniform mat4 uCameraToWorldMatrix;
            uniform mat4 uInvProjectionMatrix;
            uniform vec2 uResolution;

            uniform vec3 uLight;

            varying vec2 vTextureCoord;

            vec3 render(in vec3 light, in vec3 ro, in vec3 rd, in float resolution);

            float hash(vec2 p) { return fract(1e4 * sin(17.0 * p.x + p.y * 0.1) * (0.1 + abs(sin(p.y * 13.0 + p.x)))); }

            float noise(vec2 x) {
                vec2 i = floor(x);
                float a = hash(i);
                float b = hash(i + vec2(1.0, 0.0));
                float c = hash(i + vec2(0.0, 1.0));
                float d = hash(i + vec2(1.0, 1.0));
        
                vec2 f = fract(x);
                vec2 u = f * f * (3.0 - 2.0 * f);
                return mix(a, b, u.x) + (c - a) * u.y * (1.0 - u.x) + (d - b) * u.x * u.y;
            }
        
            float fbm(vec2 p) {
                const mat2 m2 = mat2(0.8, -0.6, 0.6, 0.8);
                float f = 0.5000 * noise(p); p = m2 * p * 2.02;
                f += 0.2500 * noise(p); p = m2 * p * 2.03;
                f += 0.1250 * noise(p); p = m2 * p * 2.01;
                f += 0.0625 * noise(p);
                return f / 0.9375;
            }


            void main(void) {
                vec3 rd = normalize(mat3(uCameraToWorldMatrix) * vec3((uInvProjectionMatrix * vec4(gl_FragCoord.xy / uResolution.xy * 2.0 - 1.0, -1.0, 1.0)).xy, -1.0));
                //gl_FragColor = vec4(rd, 1.0); return;

                vec3 pixelColor = render(uLight, uCameraToWorldMatrix[3].xyz, rd, uResolution.x);
                gl_FragColor = vec4(pixelColor, 1.0);
            }

            vec3 render(in vec3 sun, in vec3 ro, in vec3 rd, in float resolution) {
                vec3 col;
                if (rd.y < 0.0) {
                    // Ground
                    float t = -ro.y / rd.y;
                    vec2 P = ro.xz + t * rd.xz + vec2(0.5);
                    vec2 Q = floor(P);
                    // 1m^2 grid
                    P = mod(P, 1.0);
        
                    const float gridLineWidth = 0.1;
                    float res = clamp(3000.0 / resolution, 1.0, 4.0);
                    P = 1.0 - abs(P - 0.5) * 2.0;
                    float d = clamp(min(P.x, P.y) / (gridLineWidth * clamp(t + res * 2.0, 1.0, 3.0)) + 0.5, 0.0, 1.0);
                    float shade = mix(hash(100.0 + Q * 0.1) * 0.4, 0.3, min(t * t * 0.00001 / max(-rd.y, 0.001), 1.0)) + 0.6;
                    col = vec3(pow(d, clamp(150.0 / (pow(max(t - 2.0, 0.1), res) + 1.0), 0.1, 15.0))) * shade + 0.1;
                } else {
                    // Sky
                    col = vec3(0.3, 0.55, 0.8) * (1.0 - 0.8 * rd.y) * 0.9;
                    float sundot = clamp(dot(rd, sun), 0.0, 1.0);
                    col += 0.25 * vec3(1.0, 0.7, 0.4) * pow(sundot, 8.0);
                    col += 0.75 * vec3(1.0, 0.8, 0.5) * pow(sundot, 64.0);
                    col = mix(col, vec3(1.0, 0.95, 1.0), 0.5 * smoothstep(0.5, 0.8, fbm((ro.xz + rd.xz * (250000.0 - ro.y) / rd.y) * 0.000008)));
                }
                return mix(col, vec3(0.7, 0.75, 0.8), pow(1.0 - max(abs(rd.y), 0.0), 8.0));
            }
        </script>

        <script id="drawBackgroundPass-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec2 aTextureCoord;
            
            varying vec2 vTextureCoord;

            void main(void) {
                vTextureCoord = aTextureCoord;
                gl_Position = vec4(aVertexPosition, 1.0);
            }
        </script>

        <script id="drawDepthPass-fs" type="x-shader/x-fragment">
            precision mediump float;

            uniform int uIsBackground;

            void main(void) {
                gl_FragColor = vec4(gl_FragCoord.z, gl_FragCoord.z, gl_FragCoord.z, 1.0);
                
                if (uIsBackground == 1) {
                    gl_FragColor = vec4(1.0, 1.0, 1.0, 1.0);
                }
            }
        </script>

        <script id="scenePass-fs" type="x-shader/x-fragment">
            precision mediump float;

            uniform sampler2D uSamplerColor;
            uniform sampler2D uSamplerDepth;

            uniform float uCoc;

            uniform float uCoCScale;
            uniform float uCoCBias;

            uniform int uDebugCoC;

            uniform float uAperture;
            uniform float uFocallength;
            uniform float uFocalplane;
            uniform float uZNear;
            uniform float uZFar;

            varying vec2 vUv;

            void main(void) {
                vec3 scene = texture2D(uSamplerColor, vUv).rgb;

                float CoCScale = uCoCScale;
                float CoCBias = uCoCBias;


                float depthValue = texture2D(uSamplerDepth, vUv).x;
                //gl_FragColor = vec4(vec3(depthValue), 1.0); return;


                float _CoC = abs(depthValue * CoCScale + CoCBias);

                if (uDebugCoC == 1) {
                    gl_FragColor = vec4(_CoC, 0.0, 0.0, 1.0); return;
                }

                //gl_FragColor = vec4(scene, uCoc * uCoc);
                gl_FragColor = vec4(scene, _CoC * _CoC);
                //gl_FragColor = vec4(scene, 1.0);
            }
        </script>

        <script id="downsamplePass-fs" type="x-shader/x-fragment">
            precision mediump float;

            #define PI 3.1415926535897932384626433832795
            
            uniform sampler2D uSampler;
            uniform vec2 uInvViewDimensions;

            varying vec2 vUv;

            void main(void) {
                //gl_FragColor = texture2D(uSampler, vUv); return;
                // Downsample the image taking into account the blurrines of each pixel.
                
                // Sample the CoC
                vec4 coc;
                coc.x = texture2D(uSampler, vUv).w;
                coc.y = texture2D(uSampler, vUv + vec2(uInvViewDimensions.x, 0.0)).w;
                coc.z = texture2D(uSampler, vUv + vec2(0.0, uInvViewDimensions.y)).w;
                coc.w = texture2D(uSampler, vUv + vec2(uInvViewDimensions.x, uInvViewDimensions.y)).w;

                // Sample the Scene
                vec3 colorA, colorB, colorC, colorD;
                colorA = texture2D(uSampler, vUv).xyz;
                colorB = texture2D(uSampler, vUv + vec2(uInvViewDimensions.x, 0.0)).xyz;
                colorC = texture2D(uSampler, vUv + vec2(0.0, uInvViewDimensions.y)).xyz;
                colorD = texture2D(uSampler, vUv + vec2(uInvViewDimensions.x, uInvViewDimensions.y)).xyz;
                
                float blurAmount = dot(coc, vec4(1.0));

                vec3 colorTotal;
                colorTotal = colorA * coc.x;
                colorTotal += colorB * coc.y;
                colorTotal += colorC * coc.z;
                colorTotal += colorD * coc.w;

                colorTotal /= blurAmount;

                //gl_FragColor = vec4(texture2D(uSampler, vUv).w, 0.0, 0.0, 1.0); return;
                //gl_FragColor = vec4(texture2D(uSampler, vUv).xyz, 1.0); return;

                gl_FragColor = vec4(colorTotal/4.0, clamp(blurAmount/4.0, 0.0, 1.0) * (PI/2.0));
                    
                gl_FragColor = vec4(texture2D(uSampler, vUv).xyz, clamp(blurAmount/4.0, 0.0, 1.0) * (PI/2.0));
            }
        </script>

        <script id="vertAndDiagBlurPass-fs" type="x-shader/x-fragment">
            #extension GL_EXT_draw_buffers : require

            precision mediump float;

            #define PI 3.14159265322
            #define NUM_SAMPLES 16

            uniform sampler2D uSampler;

            uniform vec2 uInvViewDimensions;
            uniform float uAngle;

            varying vec2 vUv;

            // function prototypes
            vec4 BlurTexture(sampler2D tex, vec2 uv, vec2 direction);

            void main(void) {
                // Get the centre to determine the radius of the blur
                vec4 centerColor = texture2D(uSampler, vUv);
                float CoC = centerColor.a;

                // Compute the blur direction. Here, vertical.
                vec2 blurDirection = CoC * uInvViewDimensions * vec2(cos(uAngle - PI/2.0), sin(uAngle - PI/2.0));

                // Blur using the weights to bias it
                vec4 color = BlurTexture(uSampler, vUv, blurDirection) * CoC;

                // Compute the blur direction. Here, diagonal.
                vec2 blurDirection2 = CoC * uInvViewDimensions * vec2(cos(uAngle + PI / 6.0), sin(uAngle + PI / 6.0));

                // Blur using the weights to bias it
                vec4 color2 = BlurTexture(uSampler, vUv, blurDirection2) * CoC;
                
                // Output to MRT
                gl_FragData[0] = vec4(color.xyz, CoC);
                gl_FragData[1] = vec4(color2.rgb + color.xyz, CoC);
                //gl_FragData[1] = vec4(color2.rgb, CoC);
            }

            vec4 BlurTexture(sampler2D tex, vec2 uv, vec2 direction)
            {
                vec4 finalColor = vec4(0.0);
                float blurAmount = 0.0;

                // This offset is important
                uv += direction * 0.5;

                for (int i = 0; i < NUM_SAMPLES; ++i)
                {
                    vec4 color = texture2D(tex, uv + direction * float(i));
                    color *= color.a;
                    blurAmount += color.a;
                    finalColor += color;
                }

                return (finalColor / blurAmount);
            }
        </script>

        <script id="rhombiBlurPass-fs" type="x-shader/x-fragment">
            precision mediump float;

            #define PI 3.1415926535897932384626433832795
            #define NUM_SAMPLES 16

            uniform sampler2D uVerticalBlurTexture;
            uniform sampler2D uDiagonalBlurTexture;

            uniform vec2 uInvViewDimensions;

            uniform float uAngle;

            varying vec2 vUv;

            // prototype functions
            vec4 BlurTexture(sampler2D tex, vec2 uv, vec2 direction);
            
            void main(void) {
                //gl_FragColor = vec4(texture2D(uDiagonalBlurTexture, vUv).xyz, 1.0); return;
                // Get the centre to determine the radius of the blur
                vec4 centerColor = texture2D(uVerticalBlurTexture, vUv);
                vec4 centerColor2 = texture2D(uDiagonalBlurTexture, vUv);
                float coc = centerColor.a;
                float coc2 = centerColor.a;


                // Compute the blur direction. Here, diagonal
                vec2 blurDirection = coc * uInvViewDimensions * vec2(cos(uAngle + PI/6.0), sin(uAngle + PI/6.0));
                vec2 blurDirection2 = coc2 * uInvViewDimensions * vec2(cos(uAngle + 5.0 * PI/6.0), sin(uAngle + 5.0 * PI/6.0));

                // Blur using the weights to bias it
                vec4 color = BlurTexture(uVerticalBlurTexture, vUv, blurDirection) * coc;
                vec4 color2 = BlurTexture(uDiagonalBlurTexture, vUv, blurDirection2) * coc2;

                vec3 outputCol = (color.rgb + color2.rgb) * 0.5;

                //gl_FragColor = vec4((color.rgb + color2.xyz) / 2.0, 1.0); return;
                gl_FragColor = vec4(color.rgb/3.0 + (color2.rgb / 3.0), 1.0); return;
                gl_FragColor = vec4(outputCol, 1.0); return;

                // Expose. Hacked-up for this demo
                //gl_FragColor = vec4(1.0 - exp(-outputCol.rgb/0.6), 1.0);
            }

            vec4 BlurTexture(sampler2D tex, vec2 uv, vec2 direction)
            {
                vec4 finalColor = vec4(0.0);
                float blurAmount = 0.0;

                // This offset is important
                uv += direction * 0.5;

                for (int i = 0; i < NUM_SAMPLES; ++i)
                {
                    vec4 color = texture2D(tex, uv + direction * float(i));
                    color *= color.a;
                    blurAmount += color.a;
                    finalColor += color;
                }

                return (finalColor / blurAmount);
            }

        </script>

        <script id="linearDepth-fs" type="x-shader/x-fragment">
            precision mediump float;

            uniform sampler2D uSampler;
            uniform float uNear;
            uniform float uFar;

            uniform float uFocalPlane;

            varying vec2 vUv;

            float LinearizeDepth(float depth) 
            {
                float z = depth * 2.0 - 1.0; // back to NDC 
                return (2.0 * uNear * uFar) / (uFar + uNear - z * (uFar - uNear));	
            }

            void main(void) {   
                //gl_FragColor = vec4(texture2D(uSampler, vUv).xyz, 1.0); return;
                float depthValue = texture2D(uSampler, vUv).x;
                
                float linZ = LinearizeDepth(depthValue);

                gl_FragColor = vec4(vec3(linZ/uFar), 1.0);
                
                if (linZ < uFocalPlane) {
                    gl_FragColor = vec4(0.0, 1.0, 0.0, 1.0);
                }
            }

        </script>

        <script id="screenFillingTexture-fs" type="x-shader/x-fragment">
            precision mediump float;

            uniform sampler2D uSampler;

            uniform float uCoc;
        
            varying vec2 vUv;

            void main(void) {
                gl_FragColor = vec4(texture2D(uSampler, vUv).rgb, 1.0);
            }
        </script>

        <script id="screenFillingTexture-vs" type="x-shader/x-vertex">
            attribute vec3 aVertexPosition;
            attribute vec2 aTextureCoord;

            varying vec2 vUv;

            void main(void) {
                vUv = aTextureCoord;
                gl_Position = vec4(aVertexPosition, 1.0);
            }
        </script>

    </head>

    <body onload="webGLStart();">
        <div class="container">
            <canvas id="WebGLCanvas" style="border: none;" width="1280" height="720">No canvas support</canvas>
            <div id="overlay">
                <div>CoC (R/F): <span id="CoCId"></span></div>
                <div>Angle (T/G): <span id="AngleId"></span></div>
                <div>Aperture (U/J): <span id="ApertureId"></span></div>
                <div>Focal Plane (I/K): <span id="FocalplaneId"></span></div>
                <div>Focal Length (O/L): <span id="FocallengthId"></span></div>
                <div>CoCScale: <span id="CoCScaleId"></span></div>
                <div>CoCBias: <span id="CoCBiasId"></span></div>
            </div>
        </div>
        <div>
            <h1>adios</h1>
        </div>
    </body>